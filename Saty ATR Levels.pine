// Saty ATR Levels
// Copyright (C) 2022 Saty Mahajan
// Author is not responsible for your trading using this script.
// Data provided in this script is not financial advice.
//
// Special thanks to Gabriel Viana.
// Based on my own ideas and ideas from Ripster, drippy2hard, 
// Adam Sliver, and others.
//
// Features:
// - Trigger clouds for going long/short intraday @ 23.6 fib
// - Target levels including 61.8 fib and +/- 1 ATR (from previous close)
// Configuration:
// - ATR length
// - Trigger Percentage (0 - 1), default is 23.6 fib
// - Put trigger cloud
// - Call trigger cloud
// - +/- Midrange cloud (defaults to 61.8 fib when using default trigger value)
// - +/-1 ATR from previous close white clouds

//@version=4
study("Saty ATR Levels", shorttitle = "Saty ATR Levels", overlay = true)

// Options
atr_length = input(14, "ATR Length")
trigger_percentage = input(0.236, "Trigger Percentage", type=input.float)
use_todays_close = input(false, "Use Today's Close")
close_cloud_on = input(true, "Close Cloud")
close_cloud_color = input(color.white, "Close Cloud Color")
lower_trigger_cloud_on = input(true, "Lower Trigger Cloud")
lower_trigger_cloud_color = input(color.yellow, "Lower Trigger Cloud Color")
lower_middle_cloud_on = input(true, "Lower Middle Cloud")
lower_middle_cloud_color = input(color.gray, "Lower Middle Cloud Color")
lower_atr_cloud_on = input(true, "Lower ATR Cloud")
lower_atr_cloud_color = input(color.white, "Lower ATR Cloud Color")
lower_extension_cloud_on = input(false, "Lower Extension Cloud")
lower_extension_cloud_color = input(color.gray, "Lower Extension Cloud Color")
upper_trigger_cloud_on = input(true, "Upper Trigger Cloud")
upper_trigger_cloud_color = input(color.aqua, "Upper Trigger Cloud Color")
upper_middle_cloud_on = input(true, "Upper Middle Cloud")
upper_middle_cloud_color = input(color.gray, "Upper Middle Cloud Color")
upper_atr_cloud_on = input(true, "Upper ATR Cloud")
upper_atr_cloud_color = input(color.white, "Upper ATR Cloud Color")
upper_extension_cloud_on = input(false, "Upper Extension Cloud")
upper_extension_cloud_color = input(color.gray, "Upper Extension Cloud Color")
cloud_size = input(2, "Cloud Size")
show_only_today = input(true, "Show Only Today")

// Data
day_index = use_todays_close ? 0 : 1
ticker = tickerid(syminfo.prefix, syminfo.ticker, session=session.regular)
previous_close = security(ticker,"D",close[day_index],gaps = barmerge.gaps_off, lookahead = barmerge.lookahead_on)
atr = security(ticker,"D",atr(atr_length)[day_index],gaps = barmerge.gaps_off, lookahead = barmerge.lookahead_on)
lower_trigger = previous_close - (trigger_percentage * atr)
upper_trigger = previous_close + (trigger_percentage * atr)
lower_atr = previous_close - atr
upper_atr = previous_close + atr
lower_middle = (lower_trigger + lower_atr) / 2
upper_middle = (upper_trigger + upper_atr) / 2
lower_extension = (lower_atr) - (atr * 0.618)
upper_extension = (upper_atr) + (atr * 0.618)

// Add Labels
var tbl = table.new(position.top_right, 2, 1)
if (barstate.islast)
    table.cell(tbl, 0, 0, "Puts < $"+ tostring(lower_trigger, '#.##') + " | -1 ATR: $" + tostring(lower_atr, '#.##'),  bgcolor = lower_trigger_cloud_color)
    table.cell(tbl, 1, 0, "Calls > $" + tostring(upper_trigger, '#.##') + " | +1 ATR $" + tostring(upper_atr, '#.##'), bgcolor = upper_trigger_cloud_color)
    
// Plot levels
showCloud(option_on, today_on) =>
    is_today = year(timenow) == year(time) and month(timenow) == month(time) and dayofmonth(timenow) == dayofmonth(time)
    (option_on and not today_on) or (option_on and today_on and is_today)

plot(showCloud(lower_extension_cloud_on, show_only_today) ? lower_extension : na, color = color.new(lower_extension_cloud_color, 50), linewidth = cloud_size)
plot(showCloud(lower_atr_cloud_on, show_only_today) ? lower_atr : na, color = color.new(lower_atr_cloud_color, 50), linewidth = cloud_size)
plot(showCloud(lower_middle_cloud_on, show_only_today) ? lower_middle : na, color = color.new(lower_middle_cloud_color, 50), linewidth = cloud_size)
plot(showCloud(lower_trigger_cloud_on, show_only_today) ? lower_trigger : na, color = color.new(lower_trigger_cloud_color, 50), linewidth = cloud_size)
plot(showCloud(close_cloud_on, show_only_today) ? previous_close : na, color= color.new(close_cloud_color, 50), linewidth = cloud_size)
plot(showCloud(upper_trigger_cloud_on, show_only_today) ? upper_trigger : na, color = color.new(upper_trigger_cloud_color, 50), linewidth = cloud_size)
plot(showCloud(upper_middle_cloud_on, show_only_today) ? upper_middle : na, color = color.new(upper_middle_cloud_color, 50), linewidth = cloud_size)
plot(showCloud(upper_atr_cloud_on, show_only_today) ? upper_atr : na, color = color.new(upper_atr_cloud_color, 50), linewidth = cloud_size)
plot(showCloud(upper_extension_cloud_on, show_only_today) ? upper_extension : na, color = color.new(upper_extension_cloud_color, 50), linewidth = cloud_size)
